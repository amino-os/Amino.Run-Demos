apply plugin: 'java-library'

repositories {
    jcenter()
    google()
    maven { url  "https://dl.bintray.com/terryzhuo/DCAP" }
}

dependencies {
    implementation 'com.huawei.paas:sapphire-core:1.0.0'
    compile 'com.huawei.paas:java.rmi:1.0.0'
    compile 'com.huawei.paas:apache.harmony:1.0.0'
}


// Task for Stub compilation
task compileRecogStub(type: JavaCompile) {
    source = sourceSets.main.java.srcDirs
    classpath = sourceSets.main.compileClasspath
    destinationDir = sourceSets.main.output.classesDir
    options.incremental = true
}

// Task for Stub generation
task genRecogStub(type: JavaExec) {
    main = "sapphire.compiler.StubGenerator"
    classpath = sourceSets.main.runtimeClasspath
    def pkgName = 'facerecog'
    def src = "$projectDir/build/classes/java/main/facerecog/"
    def dst = "$projectDir/src/main/java/facerecog/stubs/"
    args src, pkgName, dst
}

genRecogStub.mustRunAfter compileJava
compileRecogStub.dependsOn genRecogStub
jar.dependsOn compileRecogStub


task runFaceRecognitionOMS(type: JavaExec) {
    dependsOn jar
    classpath = sourceSets.main.runtimeClasspath
    main = 'sapphire.oms.OMSServerImpl'
    args project.property('omsIp'), project.property('omsPort')
}


task runFaceRecognitionKernelServer(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'sapphire.kernel.server.KernelServerImpl'
    args project.property('kernelServerIp'), project.property('kernelServerPort'), project.property('omsIp'), project.property('omsPort')
}


// Task for running Face Recognition demo app
// Usage: ./gradlew -Dorg.gradle.java.home={/path/to/java/sdk} runDemoApp -PinferenceType={detection(default) or tracking}
task runDemoApp(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = "application.DemoAppStart"
    args project.property('omsIp'), project.property('omsPort'), project.property('kernelIpAndroid'),
            project.property('kernelServerPort'), project.property('inferenceType'), project.property('sourceType'),
            project.property('targetType')
}